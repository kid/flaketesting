# This flake was initially generated by fh, the CLI for FlakeHub (version 0.1.9)
{
  # A helpful description of your flake
  description = "My NixOS Configuration as a Flake";

  nixConfig = {
    extra-substituters = [
      "https://nix-community.cachix.org"
      "https://kidibox.cachix.org"
    ];
    extra-trusted-public-keys = [
      "nix-community.cachix.org-1:mB9FSh9qf2dCimDSUo8Zy7bkq5CX+/rkCWyvRCYg3Fs="
      "kidibox.cachix.org-1:BN875x9JUW61souPxjf7eA5Uh2k3A1OSA1JIb/axGGE="
    ];
  };

  # Flake inputs
  inputs = {
    nixpkgs.url = "github:nixos/nixpkgs/nixos-unstable";

    flake-parts.url = "github:hercules-ci/flake-parts";

    devshell.url = "github:numtide/devshell";
    devshell.inputs.nixpkgs.follows = "nixpkgs";

    treefmt-nix.url = "github:numtide/treefmt-nix";
    treefmt-nix.inputs.nixpkgs.follows = "nixpkgs";

    nix-github-actions.url = "github:nix-community/nix-github-actions";
    nix-github-actions.inputs.nixpkgs.follows = "nixpkgs";

    home-manager.url = "github:nix-community/home-manager";
    home-manager.inputs.nixpkgs.follows = "nixpkgs";
  };

  outputs = inputs @ { self, flake-parts, ... }:
    flake-parts.lib.mkFlake { inherit inputs; }
      {
        debug = true;

        imports = [
          inputs.flake-parts.flakeModules.easyOverlay
          inputs.devshell.flakeModule
          inputs.treefmt-nix.flakeModule
          ./hosts
          ./home
        ];

        systems = [
          "x86_64-linux"
          "aarch64-linux"
          "aarch64-darwin"
        ];

        flake = {
          packages = {
            x86_64-linux.hello = inputs.nixpkgs.legacyPackages.x86_64-linux.hello;
            x86_64-linux.workvm = self.nixosConfigurations.workvm.config.system.build.vm;
            aarch64-linux.workvm = self.nixosConfigurations.workvm-aarch64.config.system.build.vm;
            aarch64-darwin.workvm = self.nixosConfigurations.workvm-darwin.config.system.build.vm;
          };

          githubActions = inputs.nix-github-actions.lib.mkGithubMatrix {
            checks = inputs.nixpkgs.lib.getAttrs [ "x86_64-linux" ] self.checks;
          };
        };

        perSystem = { config, pkgs, system, self', inputs', ... }: {
          # Avoid this if possible
          # _module.args.pkgs = import inputs.nixpkgs {
          #   inherit system;
          #   overlays = [
          #     self.overlays.default
          #   ];
          # };
          # overlayAttrs = {
          #   inherit (config.packages) update-input;
          # };

          devshells.default = {
            packages = [
              pkgs.nil
              self'.packages.update-input
              config.treefmt.build.wrapper
              inputs'.home-manager.packages.default
            ] ++ builtins.attrValues config.treefmt.build.programs;
          };

          treefmt = {
            projectRootFile = "flake.nix";
            programs = {
              nixpkgs-fmt.enable = true;
              yamlfmt.enable = true;
            };
          };

          packages.update-input = pkgs.writeShellScriptBin "update-input" ''
            #!${pkgs.bash}/bin/bash
            set -euo pipefail
            input=$(nix flake metadata --json | ${pkgs.jq}/bin/jq -r '.locks.nodes.root.inputs[]' | ${pkgs.fzf}/bin/fzf)
            nix flake lock --update-input $input --commit-lock-file
          '';

          checks = self'.packages;
        };
      };
}
